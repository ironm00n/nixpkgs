# Swift needs to be built against the matching tag from the LLVM fork in the swiftlang repo.
# Ideally, it would build against upstream LLVM, but it depends on APIs that have not been upstreamed.
# For example: https://github.com/swiftlang/llvm-project/blob/901f89886dcd5d1eaf07c8504d58c90f37b0cfdf/clang/include/clang/AST/StableHash.h

{ lib,
  fetchFromGitHub,
  generateSplicesForMkScope,
  llvmPackages_19, # Needs to match the `llvmVersion` of the fork.
  swift_release,
}:

let
  swiftLlvmVersion = "17.0.0"; # From https://github.com/swiftlang/swift/blob/swift-$swiftVersion-RELEASE/utils/build_swift/build_swift/defaults.py#L51
  llvmVersion = "19.1.5"; # From https://github.com/swiftlang/llvm-project/blob/swift-$swiftVersion-RELEASE/cmake/Modules/LLVMVersion.cmake
in
(llvmPackages_19.override {
  officialRelease.version = llvmVersion;

  monorepoSrc = fetchFromGitHub {
    owner = "swiftlang";
    repo = "llvm-project";
    tag = "swift-${swift_release}-RELEASE";
    hash = "sha256-IGxg/vGlIZ2ecMRcCA1X10hNv11b0HDOLuIEkt7uNNI=";
  };

  otherSplices = generateSplicesForMkScope [
    "swiftPackages"
    "llvmPackages"
  ];

  patchesFn =
    patches:
    patches
    // {
      # Updated patch that also prevents Clang from trying to copy `clang-deps-launcher.py` to `${llvm}/bin`.
      "clang/gnu-install-dirs.patch" = [ { path = ./patches; } ];
    };
}).overrideScope
  (
    _: prev: {
      version = swiftLlvmVersion;
      release_version = llvmVersion;

      libclang = prev.libclang.overrideAttrs (old: {
        postInstall = (old.postInstall or "") + ''
          moveToOutput bin/clang-deps-launcher.py "$python"
        '';
      });

      libllvm = prev.libllvm.overrideAttrs (old: {
        patches = (old.patches or [ ]) ++ [
          # Ensure the LLVM module cache is in a writable location during builds.
          ./patches/llvm/module-cache.patch
        ];
        doCheck = false; # TODO: fix fork-specific tests that fail due to, e.g., not finding `libLLVM.dylib` during the test
        postInstall = (old.postInstall or [ ]) + ''
          # Swift relies on LLVMâ€™s private `config.h` for feature checks (e.g., for `unistd.h`).
          cp include/llvm/Config/config.h "$dev/include/llvm/Config/config.h"
        '';
      });
    }
  )
